plugins {
    id "java"
    id 'maven-publish'
    id 'jacoco'
    id "io.freefair.lombok" version "6.4.2"
    id 'org.sonarqube' version '3.4.0.2513'
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testRuntimeOnly 'org.junit.platform:junit-platform-suite-engine:1.10.0-M1'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.10.0-M1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0-M1'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            groupId = 'com.github.zakgof'
            version = version
            from(components["java"])
            pom {
                name = 'velvetdb-core'
                description = 'Java-centric NoSql database'
                url = 'https://github.com/zakgof/velvetdb'

                scm {
                    connection = 'scm:git:https://github.com/zakgof/velvetdb.git'
                    developerConnection = 'scm:git:https://github.com/zakgof/velvetdb.git'
                    url = 'https://github.com/zakgof/velvetdb/tree/master/velvetdb-core'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'zakgof'
                        name = 'Oleksandr Zakusylo'
                        email = 'zakgof@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
        mavenLocal()
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zakgof_velvetdb-core"
        property "sonar.organization", "zakgof"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", project.hasProperty('sonarlogin') ? sonarlogin : null
    }
}

tasks['sonarqube'].dependsOn test
