plugins {
    id 'java'
    id "maven"
    id "maven-publish"
    id "signing"
    id 'com.adarshr.test-logger'
}

compileJava.options.encoding = 'UTF-8'

group = 'com.github.zakgof'
archivesBaseName = 'velvetdb-xodus'

compileTestJava.dependsOn tasks.getByPath(':velvetdb-core:testClasses')

dependencies {
    implementation 'com.github.zakgof:velvetdb-core:' + version
    implementation 'com.github.zakgof:tools:' + toolsVersion
    implementation 'org.jetbrains.xodus:xodus-environment:1.3.124'
    implementation 'com.google.guava:guava:18.0'
    testImplementation project(':velvetdb-core').sourceSets.test.output
    testImplementation 'junit:junit:4.12'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        packaging 'jar'
        artifactId 'velvetdb-xodus'
        url 'https://github.com/zakgof/velvetdb'

        name 'velvetdb-xodus'
        description 'Jetbrains Xodus backend for velvetdb'

        scm {
          connection 'scm:git:https://github.com/zakgof/velvetdb.git'
          developerConnection 'scm:git:https://github.com/zakgof/velvetdb.git'
          url 'https://github.com/zakgof/velvetdb/tree/master/velvetdb-xodus'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'zakgof'
            name 'Oleksandr Zakusylo'
            email 'zakgof@gmail.com'
          }
        }
      }
    }
  }
}
